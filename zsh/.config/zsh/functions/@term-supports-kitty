#!/usr/bin/env zsh
# -*- mode: zsh; sh-indentation: 4; indent-tabs-mode: nil; sh-basic-offset: 4; -*-
# vim: ft=zsh tw=120 sw=4 sts=4 et foldmarker=[[[,]]]

typeset -gA TermSupport

@term-supports-kitty() {
    builtin emulate -LR zsh ${=${options[xtrace]:#off}:+-o xtrace}
    builtin setopt extended_glob warn_create_global typeset_silent no_short_loops rc_quotes no_auto_pushd

    if [[ ! -v TermSupport[kitty] ]]; then
        if [[ -v commands[termi] ]]; then
            command termi query keyboard-enhancement &>/dev/null
        else
            local seq;

            builtin read -rs -t 0.1 -d 'c' $'seq?\e[?u\e[c'

            [[ "$seq" == *$'\e[?'<->'u'* ]]
        fi

        (( TermSupport[kitty] = !status ))
    fi

    (( TermSupport[kitty] ))
}

@term-supports-kitty "$@"
